group 'benjamin.groehbiel.ch'
version '1.0-SNAPSHOT'

apply plugin: 'spring-boot'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'war'

sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.3.2.RELEASE'
    }
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.3.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.data', name: 'spring-data-redis', version: '1.6.0.RELEASE'
    compile group: 'redis.clients', name: 'jedis', version: '2.7.3'
    compile group: 'io.pivotal.labs', name: 'cf-env', version: '0.0.1'

    runtime group: 'org.postgresql', name: 'postgresql', version: '9.4-1201-jdbc41'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.3.2.RELEASE'
    testCompile group: 'org.fluentlenium', name: 'fluentlenium-core', version: '0.10.3'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: '2.45.0'
}

idea.module {
    outputDir = file('.idea/classes/main')
    testOutputDir = file('.idea/classes/test')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

test {
    exclude 'it/w0rd/e2e/**'
}

task buildJS(type: Exec) {
    workingDir 'src/main/js/application'
    commandLine 'sh', 'install.sh'
}

task testJS(type: Exec) {
    workingDir 'src/main/js/application'
    commandLine 'gulp', 'test-once'
}

task buildApp(type: Exec) {
    workingDir 'src/main/js/application'
    commandLine 'sh', 'deploy.sh'
}

task buildAdmin(type: Exec) {
    workingDir 'src/main/js/admin'
    commandLine 'sh', 'deploy.sh'
}

task e2e(type: Test) {
    include 'it/w0rd/e2e/**'
    reports.html.destination = file('build/reports/e2e')
}

e2e.dependsOn buildApp;

// travis runs ./gradlew check to run all tests
check.doLast {
    buildJS.execute();
    testJS.execute();
}

sourceSets {
    main {
        output.dir("$buildDir/webapp", builtBy: buildApp)
        output.dir("$buildDir/webapp", builtBy: buildAdmin)
    }
}

test {
    reports.html.destination = file('build/reports/test')
}