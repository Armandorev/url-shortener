group 'benjamin.groehbiel.ch'
version '1.0-SNAPSHOT'

apply plugin: 'spring-boot'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'war'

sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.2.6.RELEASE'
    }
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.2.6.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.data', name: 'spring-data-redis', version: '1.6.0.RELEASE'
    compile group: 'redis.clients', name: 'jedis', version: '2.7.3'
    compile group: 'io.pivotal.labs', name: 'cf-env', version: '0.0.1'

    runtime group: 'org.postgresql', name: 'postgresql', version: '9.4-1201-jdbc41'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.2.6.RELEASE'
    testCompile group: 'org.fluentlenium', name: 'fluentlenium-core', version: '0.10.3'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: '2.45.0'
}

idea.module {
    outputDir = file('.idea/classes/main')
    testOutputDir = file('.idea/classes/test')
}

test {
    exclude 'benjamin/groehbiel/ch/e2e/**'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task buildJS(type: Exec) {
    workingDir 'src/main/js'
    commandLine 'sh', 'install.sh'
}

task testJS(type: Exec) {
    workingDir 'src/main/js'
    commandLine 'gulp', 'test-once'
}

task packageJS(type: Exec) {
    workingDir 'src/main/js'
    commandLine 'sh', 'deploy.sh'
}

task e2e(type: Test) {
    include 'benjamin/groehbiel/ch/e2e/**'
    reports.html.destination = file('build/reports/e2e')
}
e2e.dependsOn packageJS;

// travis runs ./gradlew check to run all tests
check.doLast {
    buildJS.execute();
    testJS.execute();
}

sourceSets {
    main {
        output.dir("$buildDir/webapp", builtBy: packageJS)
    }
}

test {
    reports.html.destination = file('build/reports/test')
}